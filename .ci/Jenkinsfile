#!/usr/bin/env groovy

@Library('apm@current') _

pipeline {
  agent { label 'ubuntu && immutable' }
  environment {
    BASE_DIR="src/github.com/elastic/integrations"
    JOB_GIT_CREDENTIALS = "f6c7695a-671e-4f4f-a331-acdce44ff9ba"
    PIPELINE_LOG_LEVEL='INFO'
    DOCKER_REGISTRY = 'docker.elastic.co'
    DOCKER_REGISTRY_SECRET = 'secret/observability-team/ci/docker-registry/prod'
    DOCKER_IMG = "${env.DOCKER_REGISTRY}/observability-ci/integrations"
  }
  options {
    timeout(time: 1, unit: 'HOURS')
    buildDiscarder(logRotator(numToKeepStr: '20', artifactNumToKeepStr: '20', daysToKeepStr: '30'))
    timestamps()
    ansiColor('xterm')
    disableResume()
    durabilityHint('PERFORMANCE_OPTIMIZED')
    rateLimitBuilds(throttle: [count: 60, durationName: 'hour', userBoost: true])
    quietPeriod(10)
  }
  triggers {
    issueCommentTrigger('(?i).*(?:jenkins\\W+)?run\\W+(?:the\\W+)?tests(?:\\W+please)?.*')
  }
  stages {
    /**
     Checkout the code and stash it, to use it on other stages.
     */
    stage('Checkout') {
      steps {
        deleteDir()
        gitCheckout(basedir: "${BASE_DIR}")
        stash allowEmpty: true, name: 'source', useDefaultExcludes: false
      }
    }
    /**
     Checks formatting / linting.
     */
    stage('Lint') {
      steps {
        cleanup()
        withMageEnv(){
          dir("${BASE_DIR}"){
            sh(label: 'Checks formatting / linting',script: 'mage -debug check')
          }
        }
      }
    }
    /**
     Build the project from code..
     */
    stage('Build') {
      steps {
        cleanup()
        withMageEnv(){
          dir("${BASE_DIR}"){
            sh(label: 'Build',script: 'mage -debug build')
          }
        }
      }
    }
    /**
     Build the Docker image.
     */
    stage('Build Docker image') {
      when {
        not { changeRequest() }
      }
      environment {
        DOCKER_IMG_TAG = "${env.DOCKER_IMG}:${env.GIT_BASE_COMMIT}"
        DOCKER_IMG_TAG_BRANCH = "${env.DOCKER_IMG}:${env.BRANCH_NAME}"
      }
      steps {
        dir("${BASE_DIR}"){
          sh(label: 'Copy Dockerfile', script: 'cp .ci/distribution/Dockerfile .')
          pushDockerImage()
        }
      }
    }
  }
  post {
    cleanup {
      notifyBuildResult(prComment: true)
    }
  }
}

def cleanup(){
  dir("${BASE_DIR}"){
    deleteDir()
  }
  unstash 'source'
}

def pushDockerImage(){
  dockerLogin(secret: "${env.DOCKER_REGISTRY_SECRET}",
    registry: "${env.DOCKER_REGISTRY}")
  sh(label: 'Build Docker image',
    script: """docker build \
      -t ${env.DOCKER_IMG_TAG} \
      --label BRANCH_NAME=${env.BRANCH_NAME} \
      --label GIT_SHA=${env.GIT_BASE_COMMIT} \
      --label TIMESTAMP=\$(date +%Y-%m-%d_%H:%M) \
      .
  """)
  retryWithSleep(retries: 3, seconds: 5, backoff: true){
    sh(label: 'Push Docker image sha',
      script: "docker push ${env.DOCKER_IMG_TAG}")
    sh(label: 'Re-tag Docker image',
      script: "docker tag ${env.DOCKER_IMG_TAG} ${env.DOCKER_IMG_TAG_BRANCH}")
    sh(label: 'Push Docker image name',
      script: "docker push ${env.DOCKER_IMG_TAG_BRANCH}")
  }
}
